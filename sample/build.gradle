/*
 * Copyright 2016 Dmytro Zaitsev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'com.dzaitsev.rxviper'

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'

  defaultConfig {
    applicationId "com.dzaitsev.rxviper.sample"
    minSdkVersion 19
    targetSdkVersion 27
    versionCode 1
    versionName POM_VERSION
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  dataBinding {
    enabled true
  }
}

dependencies {
  annotationProcessor libraries.daggerCompiler
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation project(':rxviper')
  implementation libraries.rxAndroid
  implementation libraries.dagger
  compileOnly libraries.jsr250
  implementation libraries.appCompat
  implementation libraries.design
  implementation libraries.cardView
  implementation libraries.constraintLayout
  testImplementation libraries.junit
}

rxViper {
  packageName android.defaultConfig.applicationId // default "generated"
  useLambdas true // default false
  includeInteractor true // default true
  addMetaInfo true // default true
  includeRouter true // default true
  splitPackages true // default true

  screens {
    foo {
      packageName rxViper.packageName
      includeRouter true
      useLambdas true
      useCases {
        getItems {
          requestClass Long.class
          requestClass String.class
        }
        watchPorn {
          requestClass Void.class
          responseClass Boolean.class
        }
        buyGoods
      }
      routesTo "Home", "Settings", "Registration", "News"
    }

    bar {
      addMetaInfo false
      useLambdas false
      includeRouter false
      useCases {
          login
          post
          createUser
      }
      routesTo "Auth", "Main", "UserProfile"
    }
  }
}