/*
 * Copyright 2016 Dmytro Zaitsev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.text.SimpleDateFormat

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

version = POM_VERSION
group = POM_GROUP_ID

Date buildTimeAndDate = new Date()
ext {
  buildTime = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
  buildDate = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

def manifestAttributes = [
    'Built-By'              : System.properties['user.name'],
    'Created-By'            : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.getProperty("java.vm.version")})",
    'Build-Date'            : project.buildTime,
    'Build-Time'            : project.buildDate,
    'Specification-Title'   : POM_ARTIFACT_ID,
    'Specification-Version' : project.version,
    'Specification-Vendor'  : POM_SITE_URL,
    'Implementation-Title'  : POM_ARTIFACT_ID,
    'Implementation-Version': POM_VERSION,
    'Implementation-Vendor' : POM_SITE_URL
]

jar.manifest.attributes(manifestAttributes)

task sourcesJar(type: Jar) {
  manifest.attributes(manifestAttributes)
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  manifest.attributes(manifestAttributes)
  classifier = 'javadoc'
  from javadoc.destinationDir
}

def publicationName = POM_ARTIFACT_ID.split("-").collect { it.capitalize() }.join()

artifacts {
  archives javadocJar
  archives sourcesJar
}

publishing {
  def pomConfig = {
    licenses {
      license {
        name POM_LICENSE_NAME
        url POM_LICENSE_URL
      }
    }
    developers {
      developer {
        id POM_DEVELOPER_ID
        name POM_DEVELOPER_NAME
        email POM_DEVELOPER_EMAIL
      }
    }
    scm {
      connection POM_GIT_URL
      developerConnection POM_GIT_URL
      url POM_SITE_URL
    }
  }

  publications {
    "$publicationName"(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId POM_GROUP_ID
      artifactId POM_ARTIFACT_ID
      version POM_VERSION
      pom.withXml {
        def root = asNode()
        root.appendNode('description', POM_DESCRIPTION)
        root.appendNode('name', POM_NAME)
        root.appendNode('url', POM_SITE_URL)
        root.children().last() + pomConfig

        root.dependencies.'*'.findAll() {
          it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
            dep.name == it.artifactId.text()
          }
        }.each { it.scope*.value = 'compile' }
      }
    }
  }
}

bintray {
  dryRun = false // [Default: false] Whether to run this as dry-run, without deploying
  publish = false // [Default: false] Whether version should be auto published after an upload
  override = false // [Default: false] Whether to override version artifacts already published
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = [publicationName]
  configurations = ['archives']
  pkg {
    repo = BINTRAY_REPO
    name = BINTRAY_NAME
    desc = POM_DESCRIPTION
    websiteUrl = POM_SITE_URL
    vcsUrl = POM_GIT_URL
    licenses = ['Apache-2.0']
    labels = ['viper', 'android', 'mvp', 'presenter', 'interactor', 'router', 'view', 'viewcallbacks', 'architecture', 'cleanarchitecture' ,'mapper' ,'entity']
    publish = true
    publicDownloadNumbers = true
    version {
      desc = POM_DESCRIPTION
      gpg {
        sign = true // Determines whether to GPG sign the files. The default is false
        passphrase = project.hasProperty('bintrayGpgPassword') ? project.property('bintrayGpgPassword') :
            System.getenv('BINTRAY_GPG_PASSWORD')
        //Optional. The passphrase for GPG signing'
      }
    }
  }
}
